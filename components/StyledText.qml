pragma ComponentBehavior: Bound

import QtQuick
import Caelestia.Config
import qs.services

Text {
    id: root

    property bool animate: false

    renderType: Text.NativeRendering
    textFormat: Text.PlainText
    color: Colours.palette.m3onSurface
    // Default Qt link colour is an unreadable blue on dark surfaces; use the
    // theme accent so links in notification bodies (and anywhere else) are legible.
    linkColor: Colours.palette.m3primary
    font: Tokens.font.body.small

    Behavior on color {
        CAnim {}
    }

    Behavior on text {
        enabled: root.animate

        SequentialAnimation {
            Anim {
                target: root
                property: "opacity"
                to: 0
                type: Anim.FastEffects
            }
            PropertyAction {}
            Anim {
                target: root
                property: "opacity"
                to: 1
                type: Anim.DefaultEffects
            }
        }
    }
}
